class DefenseDrone : Actor
{
  Default
  {
	  Health 1000;
	  Radius 16;
	  Height 28;
	  Mass 1800;
	  Speed 18;
	  PainChance 255;
	  Monster;
	  +FLOAT
	  +NOGRAVITY
	  Scale 0.5;
	  MinMissileChance 5;
	  +MISSILEMORE
	  +NOBLOOD
	  Gravity 0.2;
	  //SeeSound "Radix/Drone/See";
	  //ActiveSound "Radix/Drone/See";
	  Obituary " ";
  }
  
  bool fire_cycle;
  
  void SpawnPieces()
  {
	vector3 npos = pos;
	npos.z += 25;
	let piece = ExtendedActor.Debris("BattleDrone_Piece", npos, pitch, angle,180,24,24,5,3,6);
	if (piece) piece.vel.z = random(5,12);
	if(random(1,2)==1)
	{
		let piece = ExtendedActor.Debris("RadixFlyingExplosion", npos, pitch, angle,180,24,24,5,3,6);
		if (piece) piece.vel.z = random(5,12);
	}
  }
  
  void Shoot()
  {
	 A_FaceTarget();
	 A_PlaySound("Radix/EnemyFire");
	 A_SpawnProjectile("BattleDrone_Laser", 6, 0);
  }
  
  States
  {
  Spawn:
    DEFD A 10 A_Look;
    Loop;
  See:
    DEFD A 3 A_Chase;
    Loop;
  Pain:
	DEFD A 1 
	{
		Spawn("RobotSpark",pos);
		SpawnPieces();
	}
  goto See;
  Missile:
	DEFD A 1 Shoot();
	DEFD A 12;
	TNT1 A 0 A_Jump(200,"Missile");
    Goto See;
  XDeath:
  Death:
	TNT1 A 0 
	{
		SpawnPieces();
		vector3 npos = pos;
		npos.z += 25;
		ExtendedActor.Debris("DefenseDrone_Pylon", npos, pitch, angle,90,16,16,0,14,0);
		ExtendedActor.Debris("DefenseDrone_Pylon", npos, pitch, angle,90,16,16,0,-14,0);
		A_PlaySound("Radix/Explosion",0,1);
	}
	DEFD A 3;
 	DEFD ABBB random(1,3) 
	{
		A_Quake(1,6,0,360,"");
		vector3 npos = pos;
		npos.z += 25;
		if(random(1,2)==1)A_PlaySound("Radix/Explosion",0,1);
		if(random(1,2)==1)SpawnPieces();
		let piece = ExtendedActor.Debris("Skyfire_PieceSmall", npos, pitch, angle,180,24,24,5,3,6);
		if (piece) piece.vel.z = random(5,12);
		npos.z += 15;
		let ex = ExtendedActor.Debris("RadixFlyingExplosion", npos, pitch, angle,180,48,48,8,8,12);
		if(ex) ex.vel.z = 10;
		npos.z -= 15;
		ex = Actor(ExtendedActor.Debris("RadixMissileExplosion", npos, pitch, angle,180,24,18,16,2,2));
		if(ex) ex.A_SetScale(frandom(0.6,1.0));
		ex = Actor(ExtendedActor.Debris("RadixMissileExplosion", npos, pitch, angle,180,8,24,36,2,2));
		if(ex) ex.A_SetScale(frandom(0.6,1.0));
	}
  DeathFinal:
	DEFD B -1;
    Stop;
  }
}