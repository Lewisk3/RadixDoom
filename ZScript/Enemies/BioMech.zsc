class BioMech : Actor
{
  Default
  {
	  Health 100;
	  Radius 12;
	  Height 24;
	  Mass 1800;
	  Speed 16;
	  PainChance 255;
	  Monster;
	  +FLOAT
	  +NOGRAVITY
	  Scale 0.5;
	  MinMissileChance 80;
	  +MISSILEMORE
	  +NOBLOOD
	  Gravity 0.2;
	  SeeSound "Radix/Drone/See";
	  ActiveSound "Radix/Drone/See";
	  Obituary " ";
  }
  
  void SpawnPieces()
  {
	vector3 npos = pos;
	npos.z += 25;
	let piece = ExtendedActor.Debris("Biomech_Piece", npos, pitch, angle,180,24,24,5,3,6);
	if (piece) piece.vel.z = random(5,12);
  }
  
  void Shoot()
  {
	 A_FaceTarget();
	 A_PlaySound("Radix/EnemyFire");
	 A_SpawnProjectile("BattleDrone_Laser", 10);
  }
  
  States
  {
  Spawn:
    BIOM A 10 A_Look;
    Loop;
  See:
    BIOM A 3 A_Chase;
    Loop;
  Pain:
	BIOM A 1 
	{
		Spawn("RobotSpark",pos);
		SpawnPieces();
	}
  goto See;
  Missile:
	BIOM A 1 Shoot();
	BIOM A 22;
	TNT1 A 0 A_Jump(127,"Missile");
    Goto See;
  XDeath:
  Death:
	TNT1 A 0 
	{
		SpawnPieces();
		A_PlaySound("Radix/Explosion",0,1);
	}
	BIOM A 3;
 	BIOM DDD random(1,3) 
	{
		A_Quake(1,6,0,360,"");
		vector3 npos = pos;
		npos.z += 25;
		if(random(1,2)==1)A_PlaySound("Radix/Explosion",0,1);
		if(random(1,2)==1)SpawnPieces();
		npos.z += 15;
		let ex = ExtendedActor.Debris("RadixFlyingExplosion", npos, pitch, angle,180,48,48,8,8,12);
		if(ex) ex.vel.z = 10;
		npos.z -= 15;
		ex = Actor(ExtendedActor.Debris("RadixMissileExplosion", npos, pitch, angle,180,24,18,16,2,2));
		if(ex) ex.A_SetScale(frandom(0.4,0.75));
		ex = Actor(ExtendedActor.Debris("RadixMissileExplosion", npos, pitch, angle,180,8,24,36,2,2));
		if(ex) ex.A_SetScale(frandom(0.4,0.75));
	}
  DeathFinal:
	BIOM D -1;
    Stop;
  }
}