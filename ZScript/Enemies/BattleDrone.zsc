class BattleDrone : Actor
{
  Default
  {
	  Health 200;
	  Radius 18;
	  Height 36;
	  Mass 1800;
	  Speed 12;
	  PainChance 255;
	  Monster;
	  +FLOAT
	  +NOGRAVITY
	  Scale 0.5;
	  MinMissileChance 50;
	  +MISSILEMORE
	  +MISSILEEVENMORE
	  +NOBLOOD
	  Gravity 0.2;
	  SeeSound "Radix/Drone/See";
	  ActiveSound "Radix/Drone/See";
	  Obituary " ";
  }
  
  void SpawnPieces(int amt=1)
  {
		vector3 npos = pos;
		npos.z += 25;
		if(amt >= 1)
		{
			let piece = ExtendedActor.Debris("BattleDrone_Piece", npos, pitch, angle,180,24,24,5,3,6);
			if (piece) piece.vel.z = random(5,12);
		}
		if(amt >= 2)
		{
			let arm = Actor(ExtendedActor.Debris("BattleDrone_Arm", npos, pitch, angle,180,80,80,16,2,2));
			if(arm) arm.vel.z = random(6,12);
			arm = Actor(ExtendedActor.Debris("BattleDrone_Arm", npos, pitch, angle,180,80,80,16,2,2));
			if(arm) arm.vel.z = random(6,12);
		}
		if(amt >= 3)
		{
			let arm = Actor(ExtendedActor.Debris("BattleDrone_ArmWeapon", npos, pitch, angle,90,120,120,16,6,2));
			if(arm) arm.vel.z = random(4,8);
			arm = Actor(ExtendedActor.Debris("BattleDrone_ArmWeapon", npos, pitch, angle,90,120,120,16,6,2));
			if(arm) arm.vel.z = random(4,8);
		}
  }
  
  void Shoot()
  {
	 A_FaceTarget();
	 A_PlaySound("Radix/EnemyFire");
	 A_SpawnProjectile("BattleDrone_Laser", 6, -8);
	 A_SpawnProjectile("BattleDrone_Laser", 6, 8);
  }
  
  States
  {
  Spawn:
    BATD A 10 A_Look;
    Loop;
  See:
    BATD A 3 A_Chase;
    Loop;
  Pain:
	BATD A 1 
	{
		Spawn("RobotSpark",pos);
		SpawnPieces();
	}
  goto See;
  Missile:
	BATD A 1 Shoot();
	BATD A 28;
	TNT1 A 0 A_Jump(200,"Missile");
    Goto See;
  XDeath:
  Death:
	TNT1 A 0 
	{
		SpawnPieces(3);
		A_PlaySound("Radix/Explosion",0,1);
	}
	BATD A 3;
 	BATD BBBB random(1,3) 
	{
		A_Quake(1,6,0,360,"");
		vector3 npos = pos;
		npos.z += 25;
		if(random(1,2)==1)A_PlaySound("Radix/Explosion",0,1);
		if(random(1,2)==1)Spawn("DroneExplosionSmoke",npos);
		if(random(1,3)==1)SpawnPieces(2);
		npos.z += 15;
		let ex = ExtendedActor.Debris("RadixFlyingExplosion", npos, pitch, angle,180,48,48,8,8,12);
		if(ex) ex.vel.z = 10;
		npos.z -= 15;
		ex = Actor(ExtendedActor.Debris("RadixMissileExplosion", npos, pitch, angle,180,24,18,16,2,2));
		if(ex) ex.A_SetScale(frandom(0.1,0.75));
		ex = Actor(ExtendedActor.Debris("RadixMissileExplosion", npos, pitch, angle,180,8,24,36,2,2));
		if(ex) ex.A_SetScale(frandom(0.1,0.75));
	}
  DeathFinal:
	BATD B -1;
    Stop;
  }
}

class BattleDrone_Laser : Actor
{
	Default
	{
		Projectile;
		DamageFunction ( 5 * frandom(0.8,1.4) );
		Speed 12;
		Scale 0.35;
	}
	States
	{
		Spawn:
			ERGP ABCD 3;
		loop;
		Death:
			ERGP D 1 Spawn("EnergyExplosion",pos);
		stop;
	}
}
