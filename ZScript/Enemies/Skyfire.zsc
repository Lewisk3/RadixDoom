class Skyfire : Actor
{
  Default
  {
	  Health 800;
	  Radius 16;
	  Height 28;
	  Mass 1800;
	  Speed 20;
	  PainChance 255;
	  Monster;
	  +FLOAT
	  +NOGRAVITY
	  Scale 0.5;
	  MinMissileChance 100;
	  +MISSILEMORE
	  +NOBLOOD
	  Gravity 0.2;
	  //SeeSound "Radix/Drone/See";
	  //ActiveSound "Radix/Drone/See";
	  Obituary " ";
  }
  
  bool fire_cycle;
  
  void SpawnPieces(int amt=0)
  {
	vector3 npos = pos;
	npos.z += 25;
	if(amt==0)
	{
		let piece = ExtendedActor.Debris("Skyfire_PieceSmall", npos, pitch, angle,180,24,24,5,3,6);
		if (piece) piece.vel.z = random(5,12);
	}
	if(amt==1)
	{
		let piece = ExtendedActor.Debris("Skyfire_PieceLarge", npos, pitch, angle,180,24,24,5,3,6);
		if (piece) piece.vel.z = random(5,12);
	}
  }
  
  void Shoot()
  {
	 A_FaceTarget();
	 A_PlaySound("Radix/EnemyFire");
	 A_SpawnProjectile("Skyfire_Laser", 4, fire_cycle ? 14 : -14);
	 fire_cycle = !fire_cycle;
  }
  
  States
  {
  Spawn:
    SKYF A 10 A_Look;
    Loop;
  See:
    SKYF A 3 A_Chase;
    Loop;
  Pain:
	SKYF A 1 
	{
		Spawn("RobotSpark",pos);
		SpawnPieces();
	}
  goto See;
  Missile:
	SKYF A 1 Shoot();
	SKYF A 22 
	{
		if( !target || !CheckIfTargetInLOS(90) || Distance3D(target) >= (32*32) )
		{
			return ResolveState("See");
		}
		return ResolveState(Null);
	}
    Goto Missile;
  XDeath:
  Death:
	TNT1 A 0 
	{
		SpawnPieces(1);
		SpawnPieces(1);
		A_PlaySound("Radix/Explosion",0,1);
	}
	SKYF A 3;
 	SKYF ABBB random(1,3) 
	{
		A_Quake(1,6,0,360,"");
		vector3 npos = pos;
		npos.z += 25;
		if(random(1,2)==1)A_PlaySound("Radix/Explosion",0,1);
		if(random(1,2)==1)SpawnPieces();
		npos.z += 15;
		let ex = ExtendedActor.Debris("RadixFlyingExplosion", npos, pitch, angle,180,48,48,8,8,12);
		if(ex) ex.vel.z = 10;
		ex = ExtendedActor.Debris("Skyfire_Wing", npos, pitch, angle,180,16,16,8,14,7);
		if(ex) ex.vel.z = 10;
		npos.z -= 15;
		ex = Actor(ExtendedActor.Debris("RadixMissileExplosion", npos, pitch, angle,180,24,18,16,2,2));
		if(ex) ex.A_SetScale(frandom(0.2,1.0));
		ex = Actor(ExtendedActor.Debris("RadixMissileExplosion", npos, pitch, angle,180,8,24,36,2,2));
		if(ex) ex.A_SetScale(frandom(0.2,1.0));
	}
  DeathFinal:
	SKYF B -1;
    Stop;
  }
}

class Skyfire_Laser : Actor
{
	Default
	{
		Projectile;
		DamageFunction ( 28 * frandom(0.8,1.4) );
		Speed 18;
		Scale 0.35;
	}
	States
	{
		Spawn:
			LASR A 3;
		loop;
		Death:
			LASR A 1 Spawn("EnergyExplosion",pos);
		stop;
	}
}